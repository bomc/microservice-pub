import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	// see https://github.com/spasam/spring-boot-build-info
	id 'com.pasam.gradle.buildinfo' version '0.1.3'
	
	id 'com.google.cloud.tools.jib' version '2.8.0'
	id 'com.palantir.git-version' version '0.12.3'
}

group = 'de.bomc.poc'

// Handled by plugin com.palantir.git-version
version = gitVersion()

sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2020.0.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	//implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8-config:2.0.1'
	//implementation 'org.springframework.retry:spring-retry:1.3.0'
	implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.4.8'
	implementation 'org.springdoc:springdoc-openapi-webflux-core:1.4.8'
	
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6'

	compileOnly 'org.projectlombok:lombok'
	
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.assertj:assertj-core:3.11.1'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	
	testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.0')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.7.0')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.0')
	
	testImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'
	testImplementation 'org.hamcrest:hamcrest-library:2.1'
	testImplementation 'com.jayway.jsonpath:json-path-assert:2.5.0'
	testImplementation 'commons-io:commons-io:2.6'
	
	testCompileOnly 'org.projectlombok:lombok'
	
	testAnnotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks {
    build {
        dependsOn(tasks.jibDockerBuild)
    }
}

processResources {
    filter(ReplaceTokens, tokens:[appVersion: gitVersion()])
}

task showVersion {
    doLast {
        println "\nCurrent version: ${gitVersion()}\n"
        def details = versionDetails()
        println "last tag          : ${details.lastTag}"
        println "commit distance   : ${details.commitDistance}"
        println "hash              : ${details.gitHash}"
        println "branch name       : ${details.branchName}"
        println "is clean tag      : ${details.isCleanTag}"
    }
}

jib {
    from {
    	// Base image
        image = 'adoptopenjdk/openjdk11:jre-11.0.6_10-alpine'
    }
    to {
    	// Publishing our app to the repository
        image = 'localhost:5000/bomc/' + project.name + ':' + project.version
        //credHelper = 'secretservice'
        // The image is tagged with latest and the current project.version
        tags = ['latest', project.version]
    }
    container {
    	// Add labels
    	labels = [maintainer: 'bomc', name: project.name, group: project.group]
    	// Will be deployed to /simpleApp folder.
        //appRoot = '/simpleApp'
        // Add environment
        environment = [IMAGE_VERSION: project.version]
        // The application will be limited to 360mb of memory
        jvmFlags = ['-Xms360m', '-Xmx360m', '-Dspring.profiles.active=prod']
        // This generates the following entrypoint line in the Dockerfile.
        // "Entrypoint": [java, -Xms360m, -Xmx360m, -Dspring.profiles.active=k8s, -cp, /app/resources:/app/classes:/app/libs/*, de.bomc.poc.publish.PublishApplication]
        
        // The container has port 8080 exposed
        ports = ['8181']

        //workingDirectory = '/simpleApp'
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
    allowInsecureRegistries = false
}

tasks.jibDockerBuild.dependsOn showVersion
defaultTasks 'jib'